PARSER_BEGIN(Parser)

import java.util.*;

public class Parser {

}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
}

TOKEN :
{

  < HASH : "#" >
  |
  < LET : "let" >
  |
  < TYPE : "type" >
  |
  < TRUE: "true" >
  |
  < FALSE: "false" >
  |
  <PIPE : "|">
  |
  < PLUS : "+" >
  |
  < MINUS : "-">
  |
  < STAR : "*">
  |
  < DIV : "/">
  |
  < LPAR : "(" >
  |
  < RPAR : ")" >
  |
  < LBRA : "{" >
  |
  < RBRA : "}" >
  |
  < EQUAL : "=" >
  |
  < DOT : ".">
  |
  < COLON : ":" >
  |
  < DCOLON : "::" >
  |
  < LCOLON : ":?" >
  |
  < SEMIC : ";" >
  |
  < TERM : ";;" >
  |
  < COMMA : "," >
  |
  < AND : "&&" >
  |
  < OR : "||" >
  |
  < EQ : "==" >
  |  
  < GT : ">" >
  |
  < LT : "<" >
  |
  < GE : ">=" >
  |
  < LE : "<=" >
  |
  < NEQ : "!=" >
  |
  < NOT : "~" >
  |
  < ASSIGN : ":=" >
  |
  < BOX : "box" >
  |
  < NIL : "nil" >
  |
  < DEREF : "!" >
  |
  < IF : "if" >
  |
  < ELSE : "else" >
  |
  < WHILE : "while" >
  |
  < PRINT : "print" >
  |
  < PRINTLN : "println" >
  |
  < FN : "fn" >
  |
  <ARROW : "=>">
  |
  <SARROW : "->">
  |
  <MATCH : "match">
  |
  <INT  : "int">
  |
  <UNIT  : "()">
  |
  <BOOL  : "bool">
  |
  <STRING  : "string">  
  |
  <LIST  : "list">  
  |
  <REF  : "ref">  
  |
  <UNION  : "union">  
  |
  <STRUCT  : "struct">  
  |
  < String: "\"" ( (~["\"","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\""] ) ) )* "\"" >
  |
  < Id: ["a"-"z","A"-"Z","_"] ( ["_","a"-"z","A"-"Z","0"-"9"] )* >
  |
  < Num: (["0"-"9"]) + >
}

ASTNode Start():
{ ASTNode t; }
{
    <EOF> {return null;}
  | t = Let() <TERM> { return t; }
}

ASTNode Let() :
{ Token n; 
  ASTNode t, e1, e2;
  ASTType t1 = null;
}
{ 
    (
    t = Seq()
    |
    {  List<Bind> decls  = new ArrayList<Bind>(); }
    ( (<LET>  n=<Id>
      [ <COLON> t1 = Type() ]
      <EQUAL> e1 = BA() <SEMIC>
      { decls.add(new Bind(n.image,e1, t1)); }
      )+
        e2 = Seq() { t = new ASTLet(decls, e2); }
    )
|
     {  HashMap<String,ASTType> lbl = new  HashMap<String,ASTType>(); }
   ( (<TYPE>  n=<Id>  <EQUAL> t1 = Type() <SEMIC>
      { lbl.put(n.image, t1); } )+
        e2 = Seq() { t = new ASTTypeDef(lbl, e2); }
    )

    )
   { return t; }
}

ASTNode Seq() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = SeqExp()
     ( ( op=<SEMIC> ) t2 = SeqExp() 
 		 {
		   t1 = new ASTSeq(t1, t2); 
     } 
		)*
     { return t1; }  
}

ASTNode SeqExp() :
{Token op;
  ASTNode t1, t2;
  List<ASTNode> exps = new ArrayList<ASTNode>();
}
{
     t1 = BA() { exps.add(t1); } ( ( op=<ASSIGN> ) t2 = BA()
 		 { exps.add(t2); } 
		)*
     { if(exps.size() <= 1) { return t1; }
       else{
           int size = exps.size();
           ASTAssign t = new ASTAssign(exps.get(size - 2), exps.get(size - 1));
           for(int i = size - 3; i >= 0; i--) {
                t = new ASTAssign(exps.get(i), t);
           }
           return t; } }
}

ASTNode BA() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = BM() ( ( op=<OR> ) t2 = BM() 
 		 {
		 t1 = new ASTOr(t1,t2);
		 } 
		)*
     { return t1; } 
}

ASTNode BM() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = Rel() ( ( op=<AND> ) t2 = Rel() 
 		 {
		 t1 = new ASTAnd(t1,t2);
 		 } 
		)*
     { return t1; } 
}

ASTNode Rel() :
{Token op;
  ASTNode t1, t2;}
{
     t1 = Exp() ( ( op=<EQ> | op=<GT> | op=<LT> | op=<GE> | op=<LE> | op=<NEQ>) t2 = Exp() 
     	  	  {
                     t1 = new ASTCmpOp(op,t1,t2);             
            }
		)?
     { return t1; } 
}


ASTNode Exp() :
{ Token op;
  ASTNode t1, t2; }
{
     t1=Term() ( ( op=<PLUS> | op=<MINUS> ) t2=Term() 
                 { if (op.kind == PLUS) 
                         t1 = new ASTPlus(t1,t2);
                   else  t1 = new ASTSub(t1,t2);
                 } 
               ) *
     { return t1; } 
}

ASTNode Term() :
{Token op;
Token idToken;
  ASTNode t1, t2;}
{
     t1 = List()
    (
     op=<STAR> t2 = Fact()   { t1 = new ASTMult(t1,t2); }
		 |
		 op=<DIV> t2 = Fact()  { t1 = new ASTDiv(t1,t2); }
		 |
     <DOT> <HASH> idToken = <Id> { t1 = new ASTFieldAccess(t1, idToken.image); }
     |
		 op = <LPAR> t2 = Let () <RPAR> { t1 = new ASTApp(t1,t2); } 
		)*
     { return t1; } 
}

ASTNode List():
{
  ASTNode t, e1;
  List<ASTNode> elements = new ArrayList<>();
  List<Boolean> isLazy = new ArrayList<>(); // track eager/lazy
  Token op;
}
{
    t = Fact() { elements.add(t); }
    (
      LOOKAHEAD(2) (op=<DCOLON> | op=<LCOLON>)
      e1 = Term() {
        elements.add(e1);
        isLazy.add(op.kind == ParserConstants.LCOLON);
      }
    )*
    { 
      int size = elements.size();
      if (size > 1) {
        boolean lazy = isLazy.get(isLazy.size() - 1);
        t = new ASTList(elements.get(size - 2), elements.get(size - 1), lazy);
        for(int i = size - 3; i >= 0; i--) {
          lazy = isLazy.get(i);
          t = new ASTList(elements.get(i), t, lazy);
        }
      }
    }
  { return t; }
}

ASTNode FunB(String sid) : 
{
Token n; 
ASTNode t, e1;
ASTType at;
}
{ (
  <COMMA> n = <Id>  <COLON> at = Type() 
  { e1 = FunB(n.image); 
  t = new ASTFun(sid,at, e1); }
  |
  <ARROW> <LBRA> e1 = Let() <RBRA> { t = new ASTFun(sid,null, e1);}
  )
  { return t; } 
}

ASTNode Fun() : 
{
Token n; 
ASTNode t, e1=null, e2;
ASTType at;
}
{
      <FN> n=<Id> <COLON> at = Type()
      { t = new ASTFun(n.image, at, null); e1 = t;}
      ( <COMMA> n = <Id> <COLON> at = Type() 
      { e2 = new ASTFun(n.image, at, null);
		  ((ASTFun)e1).setBody(e2); e1 = e2; })*
		  <ARROW> <LBRA> e2 = Let() <RBRA> { ((ASTFun)e1).setBody(e2); }
      { return t; } 
}


ASTNode Fact() :
{ Token n,m, idToken, t1; 
  ASTNode t, e1, e2;
  List<Bind> decls;
  ASTNode  body, alt;
  Map<String, ASTNode> fields;
} {
   (
      n=<Num> { t = new ASTInt(Integer.parseInt(n.image)); } 
    | n=<TRUE> { t = new ASTBool(true); }
    | n=<FALSE> {  t = new ASTBool(false); }
    | n=<Id>  { t = new ASTId(n.image); }
    | <BOX> t=Fact() { t = new ASTBox(t); }
    | <DEREF> t=Fact() { t = new ASTDeref(t); }
    | <MINUS> t=Fact() { t = new ASTNeg(t); }
    | <NOT> t=Fact() { t = new ASTNot(t); }
    | <IF> t=BA() <LBRA> e1=Let() <RBRA> <ELSE> <LBRA> e2=Let() <RBRA> { t = new ASTIf(t, e1, e2 ); }
    | <WHILE> t=BA() <LBRA> body=Let() <RBRA> {  t = new ASTWhile(t,body); }
    | t = Fun()
    | <PRINT> t=Fact() { t = new ASTPrint(t); }
    | <PRINTLN> t=Fact() { t = new ASTPrintln(t);  }
    | <LPAR> t=Let() <RPAR> 
    | <NIL> { t = new ASTNil(); }
    | <MATCH> t = Let() <LBRA> <NIL> <SARROW> e1 =Let()
      <PIPE> n=<Id> <DCOLON> m=<Id> <SARROW> e2 =Let() <RBRA>
      { t = new ASTMatch(t,e1,n.image,m.image,e2);}
    | <LBRA> fields = StructExprList() <RBRA> { t = new ASTStruct(fields); }
    )
   { return t; }
}

ASTType Type() : 
{
  ASTType t1, t2;
}
{        
    t1 = TypeF() 
    (<SARROW> t2 = Type() { t1 = new ASTTArrow(t1,t2);} )?
    { return t1; }
}

TypeBindList LabelList() :
{
  ASTType t;
  HashMap<String,ASTType> ll;
  Token n;
}
{   { ll = new HashMap<String,ASTType>() ; }
    ( n=<Id>  <COLON> t = Type() { ll.put(n.image,t); } 
              (<COMMA> n=<Id>  <COLON> t = Type() { ll.put(n.image,t);} )*
    )?
    { return new TypeBindList(ll); }
}

ASTType TypeF() : 
{
  ASTType t;
  TypeBindList ll;
  Token n;
}
{        
    ( 
          <INT> { t = new ASTTInt(); } 
        | n=<Id> { t = new ASTTId(n.image); } 
        | <BOOL> { t = new ASTTBool(); } 
        | <UNIT> { t = new ASTTUnit(); } 
        | <STRING> { t = new ASTTString(); } 
        | <REF> <LT> t=Type() <GT> { t = new ASTTRef(t); }
        | <LIST> <LT> t=Type() <GT> { t = new ASTTList(t); }
        | <STRUCT> <LBRA> ll = LabelList() <RBRA> { t = new ASTTStruct(ll); return t; }
        | <UNION> <LBRA> ll = LabelList() <RBRA> { t = new ASTTUnion(ll); return t; }
    )
    { return t; }
}

Map<String, ASTNode> StructExprList() :
{
  Map<String, ASTNode> fields = new HashMap<>();
  Token label;
  ASTNode expr;
}
{
  (
    <HASH> label = <Id> <EQUAL> expr = Let() { fields.put(label.image, expr); }
    ( <COMMA> <HASH> label = <Id> <EQUAL> expr = Let() { fields.put(label.image, expr); } )*
  )?
  { return fields; }
}